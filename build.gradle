// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    ext.support_lib_version = '28.0.0'
    ext.gson_version = '2.8.5'
    ext.okhttp_version = '3.12.3'

    repositories {
        // For olm library. This has to be declared first, to ensure that Olm library is not downloaded from another repo
        maven {
            url 'https://jitpack.io'
            content {
                // Use this repo only for olm library
                includeGroupByRegex "org\\.matrix\\.gitlab\\.matrix-org"
            }
        }
        // Jitsi repo
        maven {
            url "https://github.com/vector-im/jitsi_libre_maven/raw/master/releases"
        }
        google()
        jcenter()
        mavenCentral()
    }
}

apply plugin: 'org.sonarqube'

sonarqube {
    properties {
        property "sonar.projectName", "Matrix Android SDK"
        property "sonar.projectKey", "matrix.android.sdk"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectVersion", project(":matrix-sdk").android.defaultConfig.versionName
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.links.homepage", "https://github.com/matrix-org/matrix-android-sdk/"
        property "sonar.links.ci", "https://buildkite.com/matrix-dot-org/matrix-android-sdk"
        property "sonar.links.scm", "https://github.com/matrix-org/matrix-android-sdk/"
        property "sonar.links.issue", "https://github.com/matrix-org/matrix-android-sdk/issues"
        property "sonar.organization", "new_vector_ltd_organization"
        property "sonar.login", project.hasProperty("SONAR_LOGIN") ? SONAR_LOGIN : "invalid"
    }
}

project(":matrix-sdk") {
    sonarqube {
        properties {
            property "sonar.sources", project(":matrix-sdk").android.sourceSets.main.java.srcDirs
            // exclude source code from analyses separated by a colon (:)
            // property "sonar.exclusions", "**/*.*"
        }
    }
}
