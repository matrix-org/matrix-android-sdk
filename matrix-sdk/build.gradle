
apply plugin: 'com.android.library'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        // use version to define a version code.
        versionCode 904
        version "0.9.4-dev"
        versionName "0.9.4-dev"
        resValue "string", "flavor_description", "SDKApp"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
     }

    // prevent the "superClassName is empty" error for classes not annotated as tests
    tasks.withType(Test) {
        scanForTestClasses = false
        include "**/*Test.class" // whatever Ant pattern matches your test class files
    }

    signingConfigs {
        release {
            storeFile file(project.hasProperty("RELEASE_STORE_FILE") ? RELEASE_STORE_FILE : "/dummy/path")
            storePassword project.hasProperty("RELEASE_STORE_PASSWORD") ? RELEASE_STORE_PASSWORD : "dummy password"
            keyAlias project.hasProperty("RELEASE_KEY_ALIAS") ? RELEASE_KEY_ALIAS : "dummy alias"
            keyPassword project.hasProperty("RELEASE_KEY_PASSWORD") ? RELEASE_KEY_PASSWORD : "dummy password"
        }
    }

    android.libraryVariants.all { variant ->
        task("${variant.name}Docs", type: Javadoc) {
            source = variant.javaCompiler.source
            classpath = files(((Object) android.bootClasspath.join(File.pathSeparator)))
            // Comment this line else we get a gradle error:
            // Cannot create variant 'android-lint' after configuration ':matrix-sdk:debugRuntimeElements' has been resolved
            // Javadoc is still well generated without this line
            // classpath += files(variant.javaCompiler.classpath.files)

            destinationDir = file("../docs/javadoc/")
            delete "../docs/"

            options.windowTitle("Matrix Android SDK")
            options.docTitle("Matrix Android SDK")
            options.header("<b>Matrix Android SDK</b>")
            options.bottom("&copy; 2017 Vector Creations Ltd All rights reserved.")

            exclude '**/BuildConfig.java'
            exclude '**/R.java'
            failOnError false
        }
    }

    buildTypes {
        debug {
            resValue "string", "git_sdk_revision", "\"${gitRevision()}\""
            resValue "string", "git_sdk_revision_unix_date", "\"${gitRevisionUnixDate()}\""
            resValue "string", "git_sdk_revision_date", "\"${gitRevisionDate()}\""

            // Set to BODY instead of NONE to enable logging
            buildConfigField "okhttp3.logging.HttpLoggingInterceptor.Level", "OKHTTP_LOGGING_LEVEL", "okhttp3.logging.HttpLoggingInterceptor.Level.NONE"

            minifyEnabled false
        }

        release {
            resValue "string", "git_sdk_revision", "\"${gitRevision()}\""
            resValue "string", "git_sdk_revision_unix_date", "\"${gitRevisionUnixDate()}\""
            resValue "string", "git_sdk_revision_date", "\"${gitRevisionDate()}\""

            buildConfigField "okhttp3.logging.HttpLoggingInterceptor.Level", "OKHTTP_LOGGING_LEVEL", "okhttp3.logging.HttpLoggingInterceptor.Level.NONE"

            libraryVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFileName
                    if (outputFile != null && outputFile.endsWith('.aar')) {
                        output.outputFileName = outputFile.replace(".aar", "-${version}.aar")
                    }
                }
            }
        }
    }


    lintOptions {
        disable 'InvalidPackage'
        disable 'MissingTranslation'
        disable 'RestrictedApi'
    }

    repositories {
        flatDir {
            dir 'libs'
        }
    }
}

static def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}

static def gitRevisionUnixDate() {
    def cmd = "git show -s --format=%ct HEAD^{commit}"
    return cmd.execute().text.trim()
}

static def gitRevisionDate() {
    def cmd = "git show -s --format=%ci HEAD^{commit}"
    return cmd.execute().text.trim()
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    implementation(name: 'olm-sdk', ext: 'aar')

    // replace the compile 'io.pristine:libjingle:9690@aar'
    implementation(name: 'react-native-webrtc', ext: 'aar')

    // Robolectric
    testImplementation 'com.android.support.test:runner:0.5'
    testImplementation 'com.android.support.test:rules:0.5'
    testImplementation 'org.mockito:mockito-core:1.+'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.apache.maven:maven-ant-tasks:2.1.3' // fixes issue on linux/mac
    testImplementation('org.robolectric:robolectric:3.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations:25.3.1'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
}
